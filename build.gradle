buildscript {
    repositories {
        jcenter()
        maven { url 'https://palantir.bintray.com/releases/' }
    }

    dependencies {
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:0.3.27'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.4.1'
        classpath 'com.netflix.nebula:gradle-info-plugin:7.1.3'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.17.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:17.2.1'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.10.0'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'org.inferred.processors' version '3.3.0'
}

apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline'

allprojects {
    apply plugin: 'com.palantir.java-format'
    repositories {
        jcenter()
        maven { url 'https://palantir.bintray.com/releases/' }
    }

    group 'com.palantir.safe-logging'
    version gitVersion()
}

subprojects {
    apply plugin: 'java-library'

    repositories {
        jcenter()
    }

    sourceCompatibility = 1.8

    tasks.withType(Javadoc) {
        // suppress Javadoc doclint warnings in Java 8+
        if (!System.getProperty("java.version").startsWith("1.7")) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
    tasks.check.dependsOn(javadoc)

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            compileOnly 'com.google.code.findbugs:jsr305'
            annotationProcessor 'com.uber.nullaway:nullaway'
        }
        tasks.withType(JavaCompile) {
            options.errorprone.errorproneArgs += ['-XepOpt:NullAway:AnnotatedPackages=com.palantir']
        }
    })
}

