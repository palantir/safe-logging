buildscript {
    repositories {
        jcenter()
        maven { url 'https://palantir.bintray.com/releases/' }
    }

    dependencies {
        classpath 'gradle.plugin.com.palantir:gradle-circle-style:1.1.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.netflix.nebula:nebula-dependency-recommender:5.0.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.4'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.17.1'
    }
}

plugins {
    id 'com.palantir.configuration-resolver' version '0.2.0'
    id 'com.palantir.git-version' version '0.2.0'
    id 'org.inferred.processors' version '1.2.2'
}

apply plugin: 'com.palantir.baseline-config' // provides baselineUpdateConfig task
apply plugin: 'com.palantir.baseline-idea'
apply plugin: 'com.palantir.circle.style'

repositories {
    maven { url 'https://palantir.bintray.com/releases/' }
}

dependencies {
    baseline 'com.palantir.baseline:gradle-baseline-java-config:0.17.1@zip'
}

allprojects {
    group 'com.palantir.safe-logging'
    version gitVersion()

    apply plugin: 'com.palantir.configuration-resolver'
    apply plugin: 'nebula.dependency-recommender'

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }

    dependencyRecommendations {
        strategy OverrideTransitives
        propertiesFile file: project.rootProject.file('versions.props')
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-eclipse'
    apply plugin: 'com.palantir.baseline-idea'

    repositories {
        jcenter()
    }

    sourceCompatibility = 1.8

    tasks.withType(Javadoc) {
        // suppress Javadoc doclint warnings in Java 8+
        if (!System.getProperty("java.version").startsWith("1.7")) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
    tasks.check.dependsOn(javadoc)

    if (System.env.CIRCLE_TEST_REPORTS) {
        test.reports.junitXml.destination = new File(System.env.CIRCLE_TEST_REPORTS, it.getName())
    }
}

