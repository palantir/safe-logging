buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.2.1'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:1.0.1'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.4.4'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.28.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.92.0'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.12.3'
    id 'org.inferred.processors' version '3.3.0'
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline'

allprojects {
    apply plugin: 'com.palantir.java-format'
    repositories {
        mavenCentral()
        jcenter()
    }

    group 'com.palantir.safe-logging'
    version gitVersion()
}

subprojects {
    apply plugin: 'java-library'

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = 1.8

    tasks.withType(Javadoc) {
        // suppress Javadoc doclint warnings in Java 8+
        if (!System.getProperty("java.version").startsWith("1.7")) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
    tasks.check.dependsOn(javadoc)

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            compileOnly 'com.google.code.findbugs:jsr305'
            annotationProcessor 'com.uber.nullaway:nullaway'
        }
        tasks.withType(JavaCompile) {
            options.errorprone.errorproneArgs += ['-XepOpt:NullAway:AnnotatedPackages=com.palantir']
        }
    })
}

